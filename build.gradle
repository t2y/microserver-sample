buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:5.1.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'nebula.rpm' version '5.1.0'
}

group = 'com.example.microserver'
version = '0.1.0-SNAPSHOT'

ext {
    versions = [
            jackson: '2.9.7',
            jersey: '2.27',
            jetty: '9.4.14.v20181114',
    ]
}

mainClassName = 'com.example.microserver.ServerMain'

dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    compile 'com.google.guava:guava:23.0'

    // jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$versions.jackson"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$versions.jackson"

    // jersey
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-http', version: "$versions.jersey"
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: "$versions.jersey"
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: "$versions.jersey"
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: "$versions.jersey"

    // hk2
    compile group: 'org.glassfish.hk2', name: 'hk2-api', version: '2.5.0-b62'

    // jetty
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: "$versions.jetty"

    // typesafe config
    compile group: 'com.typesafe', name: 'config', version: '1.3.3'

    // lombok
    annotationProcessor('org.projectlombok:lombok:1.18.2')
    compileOnly('org.projectlombok:lombok:1.18.2')

    // logging
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.11'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.22'

    // test
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    testCompile group: 'org.glassfish.jersey.test-framework.providers', name: 'jersey-test-framework-provider-jdk-http', version: "$versions.jersey"
    testCompile group: 'org.glassfish.jersey.containers', name: 'jersey-container-jdk-http', version: "$versions.jersey"
}

repositories {
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// for debugging
tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
    }
    if (System.getProperty('HEAPDUMP', 'false') == 'true') {
        jvmArgs '-Xrunhprof:heap=all,format=b,file=stat/heapDump.hprof'
    }
    if (System.getProperty('LOWMEM', 'false') == 'true') {
        jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-Xms100m', '-Xmx100m'
    }
}
